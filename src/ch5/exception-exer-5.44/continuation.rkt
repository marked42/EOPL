#lang eopl

(require racket/lazy-require)
(lazy-require
 ["../shared/basic.rkt" (identifier?)]
 ["../shared/store.rkt" (setref reference? newref)]
 ["../shared/expression.rkt" (expression?)]
 ["environment.rkt" (extend-env environment?)]
 ["procedure.rkt" (apply-procedure/k)]
 ["interpreter.rkt" (value-of/k value-of-exps/k value-of-exps-helper/k)]
 ["value.rkt" (expval? expval->proc expval->cont)]
 ["eval.rkt" (
              eval-diff-exp
              eval-zero?-exp
              eval-if-exp
              eval-let-exp
              eval-null?-exp
              eval-cons-exp
              eval-car-exp
              eval-cdr-exp
              eval-list-exp
              eval-begin-exp
              )]
 )

(provide (all-defined-out))

(define-datatype continuation cont?
  (end-cont)
  (diff-cont (saved-cont cont?) (exp2 expression?) (saved-env environment?))
  (diff-cont-1 (saved-cont cont?) (val1 expval?))
  (zero?-cont (saved-cont cont?))
  (if-cont (saved-cont cont?) (exp2 expression?) (exp3 expression?) (saved-env environment?))
  (exps-cont (saved-cont cont?) (exps (list-of expression?)) (vals (list-of expval?)) (saved-env environment?))
  (let-cont (saved-cont cont?) (vars (list-of identifier?)) (body expression?) (env environment?))
  (call-cont (saved-cont cont?) (rands (list-of expression?)) (saved-env environment?))
  (call-cont-1 (saved-cont cont?) (rator expval?))

  (cons-cont (saved-cont cont?) (exp2 expression?) (saved-env environment?))
  (cons-cont-1 (saved-cont cont?) (val1 expval?))
  (null?-cont (saved-cont cont?))
  (car-cont (saved-cont cont?))
  (cdr-cont (saved-cont cont?))
  (list-cont (saved-cont cont?))

  (begin-cont (saved-cont cont?))

  (set-rhs-cont (saved-cont cont?) (ref reference?))

  (try-cont (saved-cont cont?) (var identifier?) (handler-exp expression?) (saved-env environment?))
  (raise-cont (saved-cont cont?))

  (throw-cont (saved-cont cont?) (exp2 expression?) (saved-env environment?))
  (throw-cont-1 (saved-cont cont?) (val1 expval?))
  )

(define (apply-cont cont val)
  (cases continuation cont
    (end-cont () val)
    (diff-cont (saved-cont exp2 saved-env)
               (value-of/k exp2 saved-env (diff-cont-1 saved-cont val))
               )
    (diff-cont-1 (saved-cont val1)
                 (apply-cont saved-cont (eval-diff-exp val1 val))
                 )
    (zero?-cont (saved-cont)
                (apply-cont saved-cont (eval-zero?-exp val))
                )
    (if-cont (saved-cont exp2 exp3 saved-env)
             (value-of/k (eval-if-exp val exp2 exp3) saved-env saved-cont)
             )
    (exps-cont (saved-cont exps vals env)
               (value-of-exps-helper/k exps (append vals (list val)) env saved-cont)
               )
    (let-cont (saved-cont vars body saved-env)
              (let ((vals val))
                (value-of/k body (eval-let-exp vars vals saved-env) saved-cont)
                )
              )
    (call-cont (saved-cont rands saved-env)
               (let ((rator val))
                 (value-of-exps/k rands saved-env (call-cont-1 saved-cont rator))
                 )
               )
    (call-cont-1 (saved-cont rator)
                 (let ((proc1 (expval->proc rator)) (rands val))
                   (apply-procedure/k value-of/k proc1 rands saved-cont)
                   )
                 )
    (cons-cont (saved-cont exp2 env)
               (value-of/k exp2 env (cons-cont-1 saved-cont val))
               )
    (cons-cont-1 (saved-cont val1)
                 (let ((val2 val))
                   (apply-cont saved-cont (eval-cons-exp val1 val2))
                   )
                 )
    (null?-cont (saved-cont)
                (apply-cont saved-cont (eval-null?-exp val))
                )
    (car-cont (saved-cont)
              (apply-cont saved-cont (eval-car-exp val))
              )
    (cdr-cont (saved-cont)
              (apply-cont saved-cont (eval-cdr-exp val))
              )
    (list-cont (saved-cont)
               (apply-cont saved-cont (eval-list-exp val))
               )
    (begin-cont (saved-cont)
                (let ((vals val))
                  (apply-cont saved-cont (eval-begin-exp vals))
                  )
                )
    (set-rhs-cont (saved-cont ref)
                  (setref ref val)
                  (apply-cont saved-cont val)
                  )
    (try-cont (saved-cont var handler-exp saved-env)
              ; returns normally
              (apply-cont saved-cont val)
              )
    (raise-cont (saved-cont)
                (apply-handler saved-cont val)
                )
    (throw-cont (saved-cont exp2 saved-env)
                (value-of/k exp2 saved-env (throw-cont-1 saved-cont val))
                )
    (throw-cont-1 (saved-cont val1)
                  (let ((cont (expval->cont val)))
                    (apply-cont cont val1)
                    )
                  )
    )
  )

; search upward linearly for corresponding try-exp
(define (apply-handler saved-cont val)
  (cases continuation saved-cont
    (end-cont () (report-uncaught-exception val))
    (diff-cont (saved-cont exp2 saved-env)
               (apply-handler saved-cont val)
               )
    (diff-cont-1 (saved-cont val1)
                 (apply-handler saved-cont val)
                 )
    (zero?-cont (saved-cont)
                (apply-handler saved-cont val)
                )
    (if-cont (saved-cont exp2 exp3 saved-env)
             (apply-handler saved-cont val)
             )
    (exps-cont (saved-cont exps vals env)
               (apply-handler saved-cont val)
               )
    (let-cont (saved-cont vars body saved-env)
              (apply-handler saved-cont val)
              )
    (call-cont (saved-cont rands saved-env)
               (apply-handler saved-cont val)
               )
    (call-cont-1 (saved-cont rator)
                 (apply-handler saved-cont val)
                 )
    (cons-cont (saved-cont exp2 env)
               (apply-handler saved-cont val)
               )
    (cons-cont-1 (saved-cont val1)
                 (apply-handler saved-cont val)
                 )
    (null?-cont (saved-cont)
                (apply-handler saved-cont val)
                )
    (car-cont (saved-cont)
              (apply-handler saved-cont val)
              )
    (cdr-cont (saved-cont)
              (apply-handler saved-cont val)
              )
    (list-cont (saved-cont)
               (apply-handler saved-cont val)
               )
    (begin-cont (saved-cont)
                (apply-handler saved-cont val)
                )
    (set-rhs-cont (ref saved-cont)
                  (apply-handler saved-cont val)
                  )
    (try-cont (saved-cont var handler-exp saved-env)
              ; returns normally
              (value-of/k handler-exp (extend-env var (newref val) saved-env) saved-cont)
              )
    (raise-cont (saved-cont)
                (apply-handler saved-cont val)
                )
    (throw-cont (saved-cont exp2 saved-env)
                (apply-handler saved-cont val)
                )
    (throw-cont-1 (saved-cont val1)
                (apply-handler saved-cont val)
                )
    )
  )

(define (report-uncaught-exception val)
  (eopl:error 'uncaught-exception "Uncaught expcetion ~s " val)
  )
